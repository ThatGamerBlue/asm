plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.0' apply false
}

apply from: 'versions.gradle'

wrapper {
    gradleVersion = "6.6"
}

ext {
    mavenUser = hasProperty('mavenUser') ? mavenUser : System.getenv('MAVEN_USER')
    mavenPass = hasProperty('mavenPass') ? mavenPass : System.getenv('MAVEN_PASS')
    mavenUrl = hasProperty('mavenUrl') ? mavenUrl : System.getenv('MAVEN_URL')
}

allprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'maven-publish'

    group 'org.spectral.asm'
    version '0.0.2'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib"

        implementation "org.ow2.asm:asm:$asm"
        implementation "org.ow2.asm:asm-commons:$asm"
        implementation "org.ow2.asm:asm-util:$asm"
        implementation "org.ow2.asm:asm-tree:$asm"
        implementation "com.google.guava:guava:$guava"

        testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek"
        testImplementation "io.mockk:mockk:$mockk"
        testImplementation "org.jetbrains.kotlin:kotlin-test"
        testImplementation "com.natpryce:hamkrest:$hamkrest"
        testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spek"
        testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect"
    }

    test {
        useJUnitPlatform {
            includeEngines 'spek2'
        }
    }
}